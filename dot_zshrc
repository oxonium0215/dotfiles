ZSHRC_DIR=~/.config/zsh
# source command override technique
function source {
  ensure_zcompiled $1
  builtin source $1
}
function ensure_zcompiled {
  local compiled="$1.zwc"
  if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
    echo "\033[1;36mCompiling\033[m $1"
    zcompile $1
  fi
}
ensure_zcompiled ~/.zshrc

# 1. Load basic environment (including PATH setup for mise/sheldon)
source $ZSHRC_DIR/nonlazy.zsh

# 2. Load plugins via sheldon cache
# (Cache is managed/generated by chezmoi)
export SHELDON_CONFIG_DIR="$ZSHRC_DIR/sheldon"
sheldon_cache="$SHELDON_CONFIG_DIR/sheldon.zsh"
if [[ -r "$sheldon_cache" ]]; then
  source "$sheldon_cache"
else
  # Fallback: if cache is missing, try to generate it if sheldon is available
  if command -v sheldon >/dev/null 2>&1; then
    sheldon source > "$sheldon_cache"
    source "$sheldon_cache"
  fi
fi
unset sheldon_cache

# 3. Load deferred/lazy configurations
zsh-defer source $ZSHRC_DIR/lazy.zsh
zsh-defer unfunction source

# Load machine-specific local configuration (not managed by chezmoi)
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local
