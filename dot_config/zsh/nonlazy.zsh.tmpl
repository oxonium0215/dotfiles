#######################################
# PATH重複防止
# -g flag ensures these remain global when sourced from a function scope
typeset -gU path PATH fpath FPATH

# Basic paths for tools needed during initialization
export PATH="$HOME/.local/share/mise/shims:$PATH"
export PATH="$HOME/.local/bin:$PATH"
export PATH="$HOME/.cargo/bin:$PATH"

#######################################
# オプション

HISTFILE=~/.zsh_history
HISTSIZE=100000
SAVEHIST=100000

# 日本語ファイル名を表示可能にする
export LANG=ja_JP.UTF-8
export LC_MESSAGES=C
setopt print_eight_bit
# beep を無効にする
setopt no_beep
unsetopt beep
# Ctrl+Dでzshを終了しない
setopt ignore_eof
# '#' 以降をコメントとして扱う
setopt interactive_comments
setopt EXTENDED_HISTORY
# 同じコマンドをヒストリに残さない
setopt hist_ignore_all_dups
# スペースから始まるコマンド行はヒストリに残さない
setopt hist_ignore_space
# ヒストリに保存するときに余分なスペースを削除する
setopt hist_reduce_blanks
setopt hist_no_store
# 同時に起動したzshの間でヒストリを共有する
setopt share_history
# ディレクトリ名だけでcdする
setopt auto_cd
# cd したら自動的にpushdする
setopt auto_pushd
# 重複したディレクトリを追加しない
setopt pushd_ignore_dups
# 高機能なワイルドカード展開を使用する
setopt extended_glob
# フローコントロールを無効にする
setopt noflowcontrol
# コマンドミスを修正
setopt correct

# 色を使用出来るようにする
autoload -Uz colors
colors

########################################
# キーバインド

# vi mode
bindkey -v
KEYTIMEOUT=2
# bindings
bindkey "^R" history-incremental-search-backward
bindkey "^S" history-incremental-search-forward
bindkey "^[[A" history-beginning-search-backward
bindkey "^[[B" history-beginning-search-forward
bindkey "^[[3~" delete-char
bindkey "^[[1~" beginning-of-line
bindkey "^[[H" beginning-of-line
bindkey "^[[4~" end-of-line
bindkey "^[[F" end-of-line
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^xe' edit-command-line

########################################
# OS 別の設定
{{ if eq .chezmoi.os "darwin" -}}
# macOS
export CLICOLOR=1
alias ls='ls -G -F'
{{ else -}}
# Linux
alias ls='ls -F --color=auto'
{{ end -}}

# プロンプト
PROMPT="%(?.%{${fg[green]}%}.%{${fg[red]}%})%n${reset_color}@${fg[blue]}%m${reset_color}(%*%) %~
%# "

# Warning injection mechanism (used by deferred scripts)
_ZSH_WARNINGS=()

function _zsh_restore_prompt() {
    PROMPT="$_ZSH_ORIGINAL_PROMPT"
    add-zsh-hook -d preexec _zsh_restore_prompt
    add-zsh-hook -d precmd _zsh_inject_warnings
}

function _zsh_inject_warnings() {
    if [[ ${#_ZSH_WARNINGS[@]} -gt 0 ]]; then
        _ZSH_ORIGINAL_PROMPT="$PROMPT"
        local combined_warnings=""
        for msg in "${_ZSH_WARNINGS[@]}"; do
            combined_warnings+="${msg}"$'\n'
        done
        PROMPT="${combined_warnings}${PROMPT}"
        _ZSH_WARNINGS=()
        
        add-zsh-hook preexec _zsh_restore_prompt
        add-zsh-hook -d precmd _zsh_inject_warnings
    fi
}

function schedule_warning() {
    _ZSH_WARNINGS+=("$1")
    autoload -Uz add-zsh-hook
    add-zsh-hook precmd _zsh_inject_warnings
}
